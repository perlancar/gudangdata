#!/usr/bin/env perl

use 5.014;
use strict;
use warnings;
use FindBin '$Bin';
use Log::ger::Screen;

use File::Slurper qw(read_text);
use Text::CSV;

sub _strip_tags {
    my $htmlref = shift;
    $$htmlref =~ s!</?[^>]+>! !gs;
}

my $columns = [qw/
                     accreditation_number
                     is_active
                     lab_name
                     address
                     phone
                     scope
                     expiry_date
                     note
                 /];
my $csv = Text::CSV->new({ binary => 1, auto_diag => 1 });
$csv->say(\*STDOUT, $columns);

for my $file (sort <$Bin/../raw/*>) {
    log_info "Processing file $file ...";
    my $html = read_text($file);
    my @recs;
    while ($html =~ m!
                         <tr[^>]*> \s*
                         (.+?)
                         </tr>
                     !gmsx) {
        push @recs, $1;
    }
  REC:
    for my $rec (@recs) {
        next if $rec =~ /<th|NAMA LABORATORIUM/;
        #log_trace $rec;

        my $row;

        $rec =~ m!\A
                  <td[^>]*> \s*(?:<p[^>]*>)?\s* (\d+)         \s*(?:</p>)?\s*                    </td> \s* # 1. nomor
                  <td[^>]*> \s*(?:<p[^>]*>)?\s* (\w+-\d+-\w+) \s*(?:</p>)?\s* (?:<p>\s*</p>)?\s* </td> \s* # 2. accreditation_number
                  .+?
                  (Nomor\s+Akreditasi\s+dicabut|Nomor\s+(?:akreditasi\s+)?dil?gabung\s+dengan\s+\S+|Nomor\s+tidak\s+digunakan(?:\s+lagi)?) # 3. note
                  .+?
                 !gimsx
                     and do {
                         log_trace "No $1 ($3)";
                         $row = [$2, "No", undef, undef, undef, undef, undef, $3];
                         goto ADD_ROW;
                     };
        $rec =~ m!\A
                     <td[^>]*> \s*(?:<p[^>]*>)?\s* (\d+)         \s*(?:</p>)?\s*                    </td> \s* # 1. nomor
                     <td[^>]*> \s*(?:<p[^>]*>)?\s* (\w+-\d+-\w+) \s*(?:</p>)?\s* (?:<p>\s*</p>)?\s* </td> \s* # 2. accreditation_number
                     <td[^>]*> \s*(?:<p[^>]*>)?\s* (.+?)         \s*(?:</p>)?\s* (?:<p>\s*</p>)?\s* </td> \s* # 3. lab_name
                     <td[^>]*> \s*(?:<p[^>]*>)?\s* (.+?)         \s*(?:</p>)?\s* (?:<p>\s*</p>)?\s* </td> \s* # 4. address
                     <td[^>]*> \s*(?:<p[^>]*>)?\s* (.*?)         \s*(?:</p>)?\s* (?:<p>\s*</p>)?\s* </td> \s* # 5. phone
                     <td[^>]*>.+?</td> \s*                # email
                     <td[^>]*> \s*(?:<p[^>]*>)?\s* ([^<]+)         \s*(?:</p>)?\s* .*?</td> \s* # 6. scope
                     <td[^>]*>(.+?)</td> \s* # 7. expiry_date
                     .*?
                 !gmsx
                     or die "Record does not match regex: $rec";
        log_trace "No $1";
        $row = [$2, "Yes", $3, $4, $5, $6, $7, ''];
        _strip_tags(\( $row->[2] )); # alamat ada mengandung tags
        _strip_tags(\( $row->[3] )); # alamat ada mengandung tags
        _strip_tags(\( $row->[4] )); # phone
        _strip_tags(\( $row->[6] )); # expiry_date juga kolom manual format bervariasi, ada tag
        for (@$row) { s/\s{2,}/ /gs; s/^\s+//; s/\s+$//; s/\xa0/ /g }

      ADD_ROW:
        $csv->say(\*STDOUT, $row);
    }
}
