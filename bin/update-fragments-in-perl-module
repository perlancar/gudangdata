#!/usr/bin/env perl

use 5.010001;
use strict;
use warnings;
use Devel::Confess;

use Cwd 'abs_path';
use Data::Dump;
use File::chdir;
use File::Slurper qw(read_text);
use FindBin '$Bin';
use Setup::File::TextFragment 'setup_text_fragment';
use String::ShellQuote;
use Text::CSV;
use Text::Fragment;
use YAML::XS qw(LoadFile);

my $script = $0;
$script =~ s!.+[/\\]!!;

die "Usage: $0 <perl-module-file>\n" unless @ARGV == 1;
my ($mod_file) = @ARGV;

my $gudangdata_dir = "$Bin/..";
$mod_file = abs_path($mod_file);

CHECK_GUDANGDATA_DIR: {
    (-d $gudangdata_dir)
        or die "ERROR: $gudangdata_dir does not exist or not a dir\n";
    (-d "$gudangdata_dir/table")
        or die "ERROR: $gudangdata_dir/table does not exist or not a dir\n";
}

my $content = read_text($mod_file);
my %datas;
my %metas;
my %revs;
INSERT_FRAGMENTS: {
    my $res = Text::Fragment::list_fragments(text => $content);
    die "ERROR: Can't list fragments in $mod_file: $res->[0] - $res->[1]\n" unless $res->[0] == 200;
    for my $f (@{ $res->[2] }) {
        next unless $f->{id} =~ /^(meta|data)-(\w+)$/;
        my $t = $2;
        my $is_meta = $1 eq 'meta';
        my $tabledir = "$gudangdata_dir/table/$t";
        (-d $tabledir)
            or die "ERROR: No such gudangdata table '$tabledir'\n";
        my $src_file;

        my $meta;
        GET_META: {
            $src_file = "meta.yaml" if $is_meta;
            $meta = $metas{$t};
            last if $meta;
            $meta = LoadFile("$tabledir/meta.yaml");
            $metas{$t} = $meta;
        }

        my $data;
      GET_DATA: {
            my @rows;
            if (-f "$tabledir/data.csv") {
                $src_file = "data.csv" if !$is_meta;
                $data = $datas{$t};
                last if $data;
                my $csv = Text::CSV->new();
                open my $fh, "<:encoding(utf8)", "$tabledir/data.csv" or die "ERROR: Can't open $tabledir/data.csv: $!\n";
                my $i = 0;
                while (my $row = $csv->getline($fh)) {
                    next if $i++ == 0 && $meta->{header};
                    push @rows, $row;
                }
            } elsif (-f "$tabledir/data.tsv") {
                $src_file = "data.tsv" if !$is_meta;
                $data = $datas{$t};
                last if $data;
                open my $fh, "<:encoding(utf8)", "$tabledir/data.tsv" or die "ERROR: Can't open $tabledir/data.tsv: $!\n";
                my $i = 0;
                while (my $line = <$fh>) {
                    next if $i++ == 0 && $meta->{header};
                    $line =~ s/\r?\n\z//;
                    push @rows, [split /\t/, $line];
                }
            } else {
                die "ERROR: No data.tsv or data.csv in $tabledir\n";
            }
            $datas{$t} = \@rows;
        }

        $revs{$t} //= do {
            local $CWD = $tabledir;
            my $output = `${\("git log -1 ".shell_quote($src_file))}`;
            $output =~ /^commit (\w+)/
                or die "ERROR: Can't parse commit ID from 'git log $src_file' output\n";
            my $rev = $1;
            $output =~ /^Date:\s*(.+)/m
                or die "ERROR: Can't parse date from 'git log $src_file' output\n";
            my $date = $1;
            "$rev ($date)";
        };

        my $varname = $f->{attrs}{varname} // ($is_meta ? 'meta' : 'data');

        my $mtime_varname = $f->{attrs}{mtime_varname};
        my ($date, $mtime);
        if (defined $mtime_varname) {
            require Date::Parse;
            $revs{$t} =~ /\((.+)\)/ or die "ERROR: Can't extract date/time from revision '$revs{$t}'\n";
            $date = $1;
            $mtime = Date::Parse::str2time($date);
            defined($mtime) or die "ERROR: Can't parse date/time '$date'\n";
        }

        my $payload = join(
            "",
            "# note: This fragment's content is generated by a script. Do not edit manually!\n",
            "# src-file: $tabledir/$src_file\n",
            "# src-revision: ", $revs{$t}, "\n",
            "# generate-date: ", scalar(gmtime), " UTC\n",
            "# generated-by: ", $script, "\n",
            "our \$$varname = " . Data::Dump::dump($is_meta ? $metas{$t} : $datas{$t}), ";\n",
            (defined $mtime_varname ? "our \$$mtime_varname = $mtime; # $date\n" : ""),
        );
        my $res = Text::Fragment::insert_fragment(text => $content, id => $f->{id}, payload => $payload);
        die "Cannot set fragment id=$f->{id}: $res->[0] - $res->[1]\n" unless $res->[0] =~ /^(200|304)$/;
        $content = $res->[2]{text};
    }
}

WRITE_MOD_FILE: {
    open my $fh, ">", $mod_file or die "ERROR: Can't open $mod_file for writing: $!\n";
    print $fh $content;
    close $fh;
}
